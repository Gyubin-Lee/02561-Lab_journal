<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Bump Mapping with Background</title>
    <script src="../../angelCommon/initShaders.js"></script>
    <script src="../../angelCommon/MV.js"></script>
</head>
<body>
    <canvas id="webgl-canvas" width="512" height="512"></canvas>

    <!-- Vertex Shader for Background -->
    <script id="vertex-shader-bg" type="x-shader/x-vertex">
        attribute vec4 a_Position;
        uniform mat4 u_Mtex;
        varying vec3 v_TexCoord;

        void main() {
            gl_Position = a_Position; // Clip-space position
            v_TexCoord = (u_Mtex * a_Position).xyz; // Calculate texture coordinates
        }
    </script>

    <!-- Fragment Shader for Background -->
    <script id="fragment-shader-bg" type="x-shader/x-fragment">
        precision mediump float;
        varying vec3 v_TexCoord;
        uniform samplerCube u_CubeMap;

        void main() {
            gl_FragColor = textureCube(u_CubeMap, normalize(v_TexCoord)); // Use cube map texture
        }
    </script>

    <!-- Vertex Shader for Sphere -->
    <script id="vertex-shader-sphere" type="x-shader/x-vertex">
        attribute vec4 a_Position;
        attribute vec3 a_Normal;
        attribute vec2 a_TexCoord;

        uniform mat4 u_Model;
        uniform mat4 u_View;
        uniform mat4 u_Projection;

        varying vec3 v_Normal;
        varying vec2 v_TexCoord;
        varying vec3 v_Position;

        void main() {
            gl_Position = u_Projection * u_View * u_Model * a_Position;
            v_Position = vec3(u_Model * a_Position);
            v_Normal = normalize(mat3(u_Model) * a_Normal);
            v_TexCoord = a_TexCoord;
        }
    </script>

    <!-- Fragment Shader for Sphere -->
    <script id="fragment-shader-sphere" type="x-shader/x-fragment">
        precision mediump float;

        varying vec3 v_Normal;
        varying vec2 v_TexCoord;
        varying vec3 v_Position;

        uniform samplerCube u_CubeMap;
        uniform sampler2D u_NormalMap;
        uniform vec3 u_EyePosition;

        vec3 rotate_to_normal(vec3 n, vec3 v) {
            float sgn_nz = sign(n.z + 1e-12);
            float a = -1.0 / (1.0 + abs(n.z));
            float b = n.x * n.y * a;

            return vec3(1.0 + n.x * n.x * a, b, -sgn_nz * n.x) * v.x +
                   vec3(sgn_nz * b, sgn_nz * (1.0 + n.y * n.y * a), -n.y) * v.y +
                   n * v.z;
        }

        void main() {
            // Get tangent space normal from normal map
            vec3 tangentNormal = texture2D(u_NormalMap, v_TexCoord).rgb * 2.0 - 1.0;

            // Convert tangent space normal to world space
            vec3 worldNormal = normalize(v_Normal);
            vec3 bumpNormal = rotate_to_normal(worldNormal, tangentNormal);

            // Compute reflection vector
            vec3 incident = normalize(v_Position - u_EyePosition);
            vec3 reflected = reflect(incident, bumpNormal);

            // Sample from cube map
            gl_FragColor = textureCube(u_CubeMap, reflected);
        }
    </script>

    <script src="Part4.js"></script>
</body>
</html>